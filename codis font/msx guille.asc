10 REM Guillem de Bergueda en versio  MSX.(c) Joan Argemi d'Abadal.1985.
20 CLS:COLOR6,11,11:CLEAR800,55296!
30 LOCATE,,0
40 POKE&HFF89,199
50 FORJ=1TO10:KEYJ,"":NEXTJ:KEY OFF
60 KEY1,"vaig nord"+CHR$(13):KEY2,"vaig sud"+CHR$(13):KEY3,"vaig est"+CHR$(13):KEY4,"vaig oest"+CHR$(13):KEY5,"farcell"+CHR$(13)
70 DIML$(30):REM implementacio
80 G(0)=32:G(1)=200:G(2)=195:G(3)=192:G(4)=222:G(5)=198:G(6)=193:G(7)=221
90 G(8)=221
100 L$(1)="3111110101111":L$(2)="3111113211111":L$(4)="3111110011111":L$(5)="3111113211001"
110 L$(7)="3111110011011":L$(9)="11111":L$(12)="211110001":L$(13)="41111140015001111":L$(18)="3111114107756":L$(21)="3111100011111":L$(23)="48888888888888888"
120 REM final lletres
130 AL=4:A$="GUILLEM":X=128:GOSUB200
140 A$="DE":X=336:GOSUB200
150 A$="BERGUEDA":X=525:GOSUB 200
160 VPOKE514,30:AL=4:A$="W":X=737:GOSUB200
170 LOCATE20,18:PRINT"(c)Joan Argemi":GOSUB 5090
180 W$=INKEY$:IFW$=""THEN180
190 GOSUB5090:GOTO 270
200 FORA=1TOLEN(A$):B=ASC(MID$(A$,A,1))-64:C=2
210 FORD=XTOX+VAL(LEFT$(L$(B),1))-1
220 FORE=DTOD+(AL-1)*40STEP40:VPOKEE,G(VAL(MID$(L$(B),C,1)))
230 C=C+1:NEXTE:NEXTD:X=D+1:NEXTA:RETURN
240 REM final del titol
260  IFMAR=1THENFA=1
270 REM del principi del guillem
280 CLS:D=0:NI=1
290 DIML1$(100),PA$(40),CO$(40),SI%(40)
300 X=INT(RND(-TIME)*82)+11
310 IFX=20ORX=30ORX=39ORX=40ORX=48ORX=49ORX=50ORX=57ORX=58ORX=59ORX=60ORX=66ORX=67THEN300
320 IFX=68ORX=69ORX=70ORX=75ORX=76ORX=77ORX=78ORX=79ORX=80ORX=84ORX=85ORX=86ORX=87THEN300
330 IFX=88 ORX=89 ORX=90 THEN 300
340 REM per provar
350 I1%=INT(RND(-TIME)*34)+1:FORJ=1TOI1%:READCX%:NEXT:I2%=33-I1%
360 FORJ=1TOI2%:READW:NEXT
370 DATA14,16,17,18,19,22,24,26,27,28,29,31,34,35,37,41,42,45,46,47,52,53,56
380 DATA 62,63,64,71,72,73,74,81,91,92
390 REM son les del cofre
400 GOSUB 1830:DI=100
410 LL=INT(LEN(MID$(L1$(CX%),10))/3)
420 L2=LEN(MID$(L1$(CX%),10))/3
430 IFL2>LLTHENR=INT(4*(L2-LL)):CL$(1)=MID$(L1$(CX%),10,LL+R)
440 IFL2>LLTHENCL$(2)=MID$(L1$(CX%),10+LL+R,LL)
450 IFL2>LLTHEN CL$(3)=MID$(L1$(CX%),10+2*LL+R,LL+R):GOTO 480
460 CL$(1)=MID$(L1$(CX%),10,LL):CL$(2)=MID$(L1$(CX%),10+LL,LL)
470 CL$(3)=MID$(L1$(CX%),10+2*LL)
480 REM
490 CLS:COLOR4,15,15:D2=3
500 PRINT"COM ET DIUS ?":PRINT"(Nom i Cognom)":GOSUB4570:N$=X$:GOSUB5090
510 FORJ=0TO250:VPOKEJ,32:NEXT
520 CLS
530 REM importantissim
540 D=D+1:D1=0:IFSO>NITHENSI%(14)=CX%
550 READSI%(6),SI%(5)
560 DATA15,76,33,61,15,76,55,82,36,65,44,76,38,51
570 REM data de seguretat
580 DATA-1,-1
590 IFSI%(6)=38ORSI%(5)=51THENRESTORE560
600 IFSI%(6)=-1ORSI%(5)=-1THENRESTORE560
610 PRINT"....................................."
620 COLOR1,15,15:D2=0
630 PRINTTAB(30)"Dia";D:PRINTN$;":"
640 IFD=11ANDD1=0ORD=17ANDD1=0ORD=24ANDD1=0ORD=29ANDD1=0ORD=33ANDD1=0THENGOSUB4350
650 REM dels mobils
660 IFG1=27ANDG2=14ANDCX%=XANDSO>1THEN PRINT:CLS:GOTO 4740
670 PRINT:PRINT"ETS a";MID$(L1$(X),9)
680 IFX=68ORX=49THENPRINT:GOTO700
690 FORJ=1TOLEN(MID$(L1$(X),9))+5:PRINT"~";:NEXT:PRINT
700 FORJ=5 TO 40
710 IFSI%(J)=XTHEN :PRINT"POTS VEURE :":PRINT"~~~~~~~~~~":PRINTCO$(J)
720 NEXTJ:PRINT
730 REM roca
740 IFG1=12ANDG2=8ANDX=3THENPRINT:PRINT"La inscripcio posa :";
750 IFG1=12ANDG2=8ANDX=3THENPRINTCHR$(34);"La vall que ";
760 IFG1=12ANDG2=8ANDX=3THENPRINT"veusalla baix, va cap al nord.";CHR$(34)
770 IFX=95THENPRINT"Et trobes un moro que et diu :";CHR$(34)CL$(2)CHR$(34):SO=SO+1
780 IFX=85THENPRINT"Un morisc et canta a l'orella :";
790 IFX=85THEN PRINT" esta prop de";MID$(L1$(CX%+1),9):SO=SO+1
800 IFG1=17ANDG2=32ANDX=40ANDSI%(18)=-1THENGOTO 830
810 IFG1=17ANDG2=32ANDX=40ANDSI%(17)=-1THENGOTO 830
820 GOTO850
830 PRINT"que abans de morir et diu agonitzant ";CHR$(34)CL$(1)CHR$(34):SO=SO+1
840 SI%(32)=0
850 IFG1=3ANDG2=23ANDX=68ANDSI%(7)=<1THEN870
860 GOTO 880
870 PRINT"i el notari et diu :";CHR$(34)CL$(1)CHR$(34):SO=SO+1
880 IFG1=34ANDG2=30ANDX=16THEN 900
890 GOTO920
900 PRINT"I l'Arondeta et contesta dient suau- ment :";
910 PRINTCHR$(34);CL$(2);CHR$(34):SO=SO+1
920 IFG1=33ANDG2=29ANDX=27ANDSI%(7)=<1THEN940
930 GOTO950
940 PRINT"que et respon :";CHR$(34)CL$(2);CHR$(34):SO=SO+1
950 IFX=57ANDG2=4THENMAR=0:GOTO970
960 GOTO1000
970 IFRIGHT$(CO$(19),1)><"a"THENSI%(19)=0
980 IFRIGHT$(CO$(20),1)><"a"THEN SI%(20)=0
990 PRINT"Un temporal ha enfonsat el vaixell i us ha deixat a la platja ..":PB=0
1000 REM bisbe
1010 IF G1=7 AND G2=36 AND X=14 ANDDI>0THENPRINT"I et respon : ";CHR$(34);
1020 ID=LEN(L1$(CX%-10))-8:IFG1=7ANDG2=36ANDX=14ANDDI>0THENPRINT"PAX circa ";
1030 IFG1=7ANDG2=36ANDX=14ANDDI>0THENPRINTMID$(L1$(CX%-10),10,ID);
1040 IFG1=7ANDG2=36ANDX=14ANDDI>0THENPRINTCHR$(34)
1050 IFG1=7ANDG2=36ANDX=14ANDDI>0THENSO=SO+1:DI=0
1060 REM gana
1070 IFSI%(5)=XTHENGA=1
1080 IFGA=1ANDSI%(22)<0THENPRINT"pero t'has afartat amb els queviures que portaves."
1090 IFGA=1ANDSI%(22)<0THENSI%(22)=35:GA=0:SI%(5)=SI%(5)+5
1100 IFSI%(5)=XANDSI%(22)>0ANDGA=1THENGOSUB5000
1110 REM llibre
1120 IFG1=12ANDG2=10ANDSI%(10)<0THENPRINT"I ara recordes, el titol del capitol que ";
1130 IFG1=12ANDG2=10ANDSI%(10)<0THENGOTO1150
1140 GOTO1170
1150 PRINT"diu : ODA a ";:FORJ=1TOSA
1160 PRINTMID$(SO$,SA-J+1,1);:NEXT:SO=SO+1
1170 REM bandoler
1180 IFX=SI%(6)THEN BA=1
1190 IFBA=1AND SI%(18)=-1THEN PRINT"Has fet fugir al bandoler !":BA=0:GOTO1430
1200 IFBA=1AND SI%(17)=-1THEN PRINT"Has fet fugir al bandoler !":BA=0:GOTO1430
1210 IFBA=1THEN GOSUB4950
1220 REM anell
1230 IFG1=27ANDG2=12ANDSI%(12)<0THENPRINT:PRINT"Dintre l'anell, hi trobes l'escut ";
1240 IFG1=27ANDG2=12ANDSI%(12)<0THENPRINT"he-raldic de";MID$(L1$(CX%),9);" ...":SO=SO+1
1250 REM botafumeiro
1260 IFX=77THENPRINT:PRINT"a dintre la catedral, el botafumeiro escriu amb fum : ";
1270 IFX=77THEN GOTO1290
1280 GOTO1310
1290 FORJ=1TOSASTEP2
1300 PRINTMID$(SO$,J,1);" ";:NEXT:SO=SO+1
1310 IFG1=36ANDG2=29THENPRINT:PRINT"Un dels oients .. et dona diners i etdiu :";
1320 IFG1=36ANDG2=29THENPRINTCHR$(34);CL$(3);CHR$(34):DI=DI+10:SO=SO+1
1330 REM barca
1340 IFG1=15ANDG2=19ANDMAR=1THENPRINT"Ets a dalt de la teva barca a punt de";
1350 IFG1=15ANDG2=19ANDMAR=1THENPRINT"navegar ...":PB=1
1360 IFG1=15ANDG2=20ANDMAR=1THENPRINT"Ets a dalt del teu galio, a punt de  nave";
1370 IFG1=15ANDG2=20ANDMAR=1THENPRINT"gar ...":PB=1:REM lo del fossil
1380 IFG1=13ANDG2=15ANDSI%(15)<0THENPRINT"Darrera el fossil, veus uns signes   rars :";
1390 IFG1=13ANDG2=15ANDSI%(15)<0THENGOTO1410
1400 GOTO1440
1410 FORJ=1TOSA:PRINTCHR$(203+J);CHR$(205+J);MID$(SO$,J,1);
1420 NEXT J:PRINTCHR$(203+J+1);:PRINTCHR$(204+J+2):REM final coses importants
1430 REM
1440 PRINT:PRINT"POTS ANAR":PRINT"~~~~~~~~~":PRINT" ";
1450 IFVAL(LEFT$(L1$(X),2))>0THENPRINT"nord ";
1460 IFVAL(MID$(L1$(X),3,2))>0THENPRINT"sud ";
1470 IFVAL(MID$(L1$(X),5,2))>0THENPRINT"est ";
1480 IFVAL(MID$(L1$(X),7,2))>0THENPRINT"oest ";
1490 PRINT:PRINT:PRINT"QUE FAS ...?":PRINT"~~~~~~~"
1500 GOSUB5090:GOSUB4570
1510 E=0:FORJ=1TOLEN(X$):IFMID$(X$,J,1)=" "THEN E=J
1520 NEXT J
1530 IFE=0THENE=LEN(X$)+2
1540 REM
1550 REM comrovador de paraules d'accio
1560 PRINT:G1=0:G2=0
1570 FORJ=0TO40
1580 IFRIGHT$(X$,4)=RIGHT$(PA$(J),4)THENG2=J
1590 IFLEFT$(X$,4)=LEFT$(PA$(J),4)THENG1=J
1600 NEXTJ:T=G1+G2
1610 IFG1=23THENG1=27
1620 IFG1=35ORG1=19THENG1=3
1630 IFG1=28THENG1=13
1640 IFG1=25THENG1=15
1650 IFG1=26THENG1=9
1660 IFG1=29THENG1=10
1670 IFG1=16THENG1=8
1680 IFG1=24THENG1=17
1690 IFG1=11ANDG2=11THENPRINT"ESTAS PORTANT :":PRINT"~~~~~~~~~~~~~":PRINT:G2=1:GOTO1710
1700 GOTO1750
1710 IFDITHENPRINT"DINERS,";
1720 FORJ=5TO40:IFSI%(J)=-1THENPRINTMID$(PA$(J),7);",";
1730 NEXTJ:IFMAR=1THENPRINT"Titol Barca,";
1740 PRINT:PRINT:PRINT:FORJ=1TO1500:NEXTJ:GOTO600
1750 IFG1=0ANDG2=0THENPRINT"No entenc que vol dir ";CHR$(34);X$;CHR$(34):GOTO600
1760 IFG1>0ANDG2=0THENPRINTLEFT$((X$),E);"que ?"
1770 IFG>0ANDG2=0THENGOTO600
1780 IFG1=0ANDT>0THENPRINT"No entenc que vol dir ";CHR$(34);LEFT$((X$),E);CHR$(34)
1790 IFG1=0ANDT>0THENGOTO600
1800 GOTO3170
1810 REM a les rutines d'accio
1820 REM dades
1830 REM
1840 L1$(1)="00000207l Pirineu, perdut"
1850 L1$(2)="00000301l Pirineu, perdut"
1860 L1$(3)="00000402l Pirineu, perdut"
1870 L1$(4)="00000503l Pirineu, perdut"
1880 L1$(5)="00000604l Pirineu, perdut"
1890 L1$(6)="00000705l Pirineu, perdut"
1900 L1$(7)="00000106l Pirineu, perdut"
1910 L1$(11)="01211276l bosc"
1920 L1$(12)="02221311l bosc"
1930 L1$(13)="03231412l bosc"
1940 L1$(14)="04241513 la SEU d'URGELL"
1950 L1$(15)="05251614l bosc"
1960 L1$(16)="06261715 BERGA"
1970 L1$(17)="07271816 RIPOLL"
1980 L1$(18)="07281917 OLOT"
1990 L1$(19)="07293018 ROSES"
2000 L1$(21)="11312276L bosc"
2010 L1$(22)="12322321 TREMP"
2020 L1$(23)="13332422l bosc"
2030 L1$(24)="14342523 SOLSONA"
2040 L1$(25)="15352624 CARDONA, davant del castell"
2050 L1$(26)="16362725 MOIA"
2060 L1$(27)="17372826 VIC"
2070 L1$(28)="18382927 GIRONA"
2080 L1$(29)="19399428 PALAMOS"
2090 L1$(30)="93390000 la platja, perdut"
2100 L1$(31)="21413276 LLEIDA"
2110 L1$(32)="22423331l bosc"
2120 L1$(33)="23433432l bosc"
2130 L1$(34)="24443533 CALAF"
2140 L1$(35)="25453634 MANRESA"
2150 L1$(36)="26463735l bosc"
2160 L1$(37)="27473836l MONTSENY"
2170 L1$(38)="28573937l bosc"
2180 L1$(39)="30570000 la platja, perdut"
2190 L1$(40)="25252525 una sala del castell"
2200 L1$(41)="31514276 FRAGA"
2210 L1$(42)="32524341 BORGES BLANQUES"
2220 L1$(43)="33534442l bosc"
2230 L1$(44)="34544543l bosc"
2240 L1$(45)="35554644 MONTSERRAT"
2250 L1$(46)="36564745 TERRASSA"
2260 L1$(47)="37573946 BADALONA"
2270 L1$(49)="00230000 dintre la cova,  magatzem del  bandoler"
2280 L1$(51)="41615276l bosc"
2290 L1$(52)="42625351 MONTBLANC"
2300 L1$(53)="43635452 SANTES CREUS"
2310 L1$(54)="44645553l bosc"
2320 L1$(55)="45655654l bosc"
2330 L1$(56)="46665755 BARCELONA"
2340 L1$(57)="39660000 la platja, perdut"
2350 L1$(61)="51716276l bosc"
2360 L1$(62)="52726361 CIURANA"
2370 L1$(63)="53736462 VALLS"
2380 L1$(64)="54746563 EL VENDRELL"
2390 L1$(65)="55756664l bosc"
2400 L1$(66)="57750000 la platja, perdut"
2410 L1$(71)="61817276 MORA d'EBRE"
2420 L1$(72)="62827371 FALSET"
2430 L1$(73)="63837472 REUS"
2440 L1$(74)="64939473 TARRAGONA"
2450 L1$(75)="66930000 la platja, perdut"
2460 L1$(81)="71918276 GANDESA"
2470 L1$(82)="72928381l bosc"
2480 L1$(83)="73937582l bosc"
2490 L1$(91)="81009276 TORTOSA"
2500 L1$(92)="82009391 AMPOSTA"
2506 IFG1=36ANDG2><29THENPRINT"Estas perdent el temps."
2510 L1$(93)="75200000 la platja, perdut"
2520 L1$(67)="68040000 L'AQUITANIA"
2530 L1$(96)="00670000 ANGLATERRA, i a la cort del reiRicard"
2540 L1$(97)=" Cor de LLeo ..."
2550 L1$(68)=L1$(96)+L1$(97)
2560 L1$(76)="00003177l cami de SANTIAGO"
2570 L1$(77)="00007600 SANTIAGO de COMPOSTELA"
2580 L1$(85)="91000000l PAIS VALENCIA"
2590 L1$(94)="00009557 alta mar"
2600 L1$(95)="00000094 MALLORCA, i al arribar :"
2610 REM
2620 REM verbs
2630 REM
2640 PA$(1)="vaig  nord"
2650 PA$(2)="       sud"
2660 PA$(3)="signo est"
2670 PA$(4)="       oest"
2680 CO$(5)="que estas defallint de gana ..."
2690 PA$(6)="menjo bandoler":CO$(6)="Un bandoler que t'intenta robar ...":SI%(6)=15
2700 PA$(7)="pago  ploma":CO$(7)="Una fabulosa ploma xinesa per escriu-re ..."
2710 SI%(7)=56
2720 PA$(8)="segueixinscripcio":CO$(8)="":SI%(8)=0
2730 PA$(9)="agafo  penya-segat":CO$(9)="Un ferestec penya-segat rocos ..."
2740 SI%(9)=30
2750 CO$(37)="Un ferestec penya-segat rocos ...":SI%(37)=75
2760 PA$(10)="deixo llibre":CO$(10)="Un meravellos llibre de versos ...":SI%(10)=72
2770 PA$(11)="farcell"
2780 PA$(12)="llegei anell":CO$(12)="Un intrigant anell amb una ametista ...":SI%(12)=41
2790 PA$(13)="examino"
2800 PA$(14)="    cofre":CO$(14)="Un cofre meravellos, tancat"
2810 PA$(15)="pujo   fossil"
2820 CO$(15)="Un fossil, dels utilitzats com a mo- neda prehistorica ..."
2830 SI%(15)=49
2840 PA$(16)="baixo  escala":CO$(16)="Una escala de corda ...":SI%(16)=46
2850 PA$(17)="mato   basto":CO$(17)="Un basto de noguera, amb el puny de  plata .."
2860 SI%(17)=22
2870 PA$(18)="entro  punyal":CO$(18)="Un gran punyal corbat, de plata ..."
2880 SI%(18)=53
2890 PA$(19)="firmo  barca":CO$(19)="Una bonica barca de pescadors, posadaa la venda ...":SI%(19)=29
2900 PA$(20)="        galio":CO$(20)="Un magnific galio, en venda":SI%(20)=74
2910 PA$(21)="     torrent"
2920 PA$(22)="entro queviures":CO$(22)="Uns quans queviures de tota mena ..":SI%(22)=35
2930 PA$(23)="destapodocuments":CO$(23)="Uns documents, que et presenten ...":SI%(23)=68
2940 PA$(24)="assassicova":CO$(24)="Una gran cova, amagada ":SI%(24)=50
2950 PA$(25)="m'enfilo"
2960 CO$(25)= "Un torrent de montanya que te la di- reccio del vent ..."
2970 SI%(25)=6
2980 PA$(26)="prenc  vall":SI%(26)=3
2990 CO$(26)="Una grandiosa roca amb una inscripcioal costat del cami ..."
3000 PA$(27)="obro  cova":CO$(27)="Una gran cova, amagada per la vegeta-cio ...":SI%(27)=23
3010 PA$(28)="observo      "
3020 PA$(29)="abandonsirventes":CO$(29)="":SI%(29)=0
3030 PA$(30)="descansArondeta":CO$(30)="a la teva estimada Arondeta ...":SI%(30)=16
3040 PA$(31)="compro Hugh de Mataplana"
3050 CO$(31)="al teu conegut Hugh de Mataplana ...":SI%(31)=27
3060 PA$(32)="venc    Guillem Folch de Cardona"
3070 CO$(32)="al teu enemic vital, el Comte de Car-dona ...":SI%(32)=40
3080 PA$(33)="escric Bisbe de la Seu"
3090 CO$(33)="al Bisbe de la Seu,  que  et  demana el  delma ...":SI%(33)=14
3100 PA$(34)="festejocastell"
3110 PA$(35)="rubricbarquer":CO$(35)="al barquer de l'Ebre que vol cobrar ..."
3120 PA$(38)="       barquer":CO$(38)="al barquer de l'Ebre que vol cobrar ..."
3130 SI%(35)=91:SI%(38)=92
3140 PA$(36)="recito delma"
3150 SO$=MID$(L1$(CX%),10):SA=LEN(SO$)
3160 RETURN
3170 REM
3180 REM
3190 REM prohibidora
3200 IFG2=1ANDVAL(LEFT$(L1$(X),2))=0THENPRINT"No pots fer-ho":GOTO600
3210 IFG2=2ANDVAL(MID$(L1$(X),3,2))=0THENPRINT"T'es impossible":GOTO600
3220 IFG2=3ANDVAL(MID$(L1$(X),5,2))=0THENPRINT"No t'hi empenyis":GOTO600
3230 IFG2=4ANDVAL(MID$(L1$(X),7,2))=0THENPRINT"No pots anar-hi":GOTO600
3240 REM dinamica
3250 IFG1=1ANDG2=1THENX=VAL(MID$(L1$(X),1,2))
3260 IFG1=1ANDG2=2THENX=VAL(MID$(L1$(X),3,2))
3270 IFG1=1ANDG2=3THENX=VAL(MID$(L1$(X),5,2))
3280 IFG1=1ANDG2=4THENX=VAL(MID$(L1$(X),7,2))
3290 REM diccionari
3300 REM agafo i compro
3310 IFG1=9THEN3340
3320 IFG1=31THEN3400
3330 GOTO3500
3340 IFG2>18ANDG2<21THENPRINT"No et deixen !":GOTO4270
3350 IFG2=30ORG2=31ORG2=32ORG2=33ORG2=60ORG2=35ORG2=38THENPRINT"No es deixa !":GOTO4270
3360 IFG2=26ORG2=9ORG2=24THENPRINT"No pots !":GOTO4270
3370 GOSUB 4910:IFFA>1THENPRINT"No pots carregar tant !":GOTO4270
3380 FORJ=5TO40:IFSI%(G2)=XTHENSI%(G2)=-1:GOTO4270
3390 NEXT:PRINT"No hi ha cap ";RIGHT$(X$,LEN(X$)-E):GOTO4270
3400 IFDI<25THENPRINT"No tens prou diners !":GOTO4270
3410 IFG2=19THENPRINT"Es teva !":DI=0:MAR=1:CO$(19)=LEFT$(CO$(19),29):SI%(19)=29
3420 IFG2=20THENPRINT"Es teu !":DI=0:MAR=1:CO$(20)=LEFT$(CO$(20),17):SI%(20)=74
3430 IF G2=19 OR G2=20 THEN GOTO 4270
3440 IFG2=30ORG2=31ORG2=32ORG2=33ORG2=6THENPRINT"No es ven !":GOTO4270
3450 IFG2=26ORG2=9THENPRINT"No pots !":GOTO4270
3460 GOSUB4910:IFFA>1THENPRINT"No t'ho pots emportar !":GOTO4270
3470 FORJ=5TO40:IFSI%(G2)=XTHENSI%(G2)=-1:DI=D-25:GOTO4270
3480 NEXT:PRINT"NO HI HA CAP ";RIGHT$(X$,LEN(X$)-E):GOTO 4270
3490 REM deixo i el venc
3500 IFG1=10THEN3530
3510 IFG1=32THEN3560
3520 GOTO3650
3530 IFG2=20ORG2=19THENPRINT"Quin pes t'has tret !":D=DI+50
3540 FORJ=5TO40:IFSI%(G2)=-1THENSI%(G2)=X:GOTO4270
3550 NEXT:PRINT"No portes cap ";RIGHT$(X$,LEN(X$)-E):GOTO4270
3560 GOSUB4910:IFFA<1THENPRINT"No tens res per vendre !":GOTO4270
3570 IFX=11ORX=12ORX=13ORX=15ORX=21ORX=23ORX=32THENPRINT"No tens a qui vendre !":GOTO 4210
3580 IFX=33ORX=36ORX=38ORX=43ORX=44ORX=51THENPRINT"No tens a qui vendre !":GOTO 4270
3590 IFX=54 OR X=5ORX=82ORX=83THENPRINT"No tens a qui vendre !":GOTO 4270
3600 FORJ=5TO40:IFSI%(G2)=-1THENSI%(G2)=X:DI=D+10:GOTO4270
3610 IFMAR=1THENDI=DI+50:MAR=0
3620 IF RIGHT$(CO$(19),1)><"a"THEN CO$(19)=CO$(19)+", en venda":GOTO4270
3630 IF RIGHT$(CO$(20),1)><"a"THEN CO$(20)=CO$(20)+", en venda":GOTO4270
3640 NEXT:PRINT"No tens cap ";RIGHT$(X$,LEN(X$)-E):GOTO4270
3650 REM aqui s'acaben les rutines basiques
3660 REM arondeta
3670 IFG1=34ANDG2>30ORG1=34ANDG2<3THENPRINT"No es deixa !":GOTO4270
3680 IFG1=34ANDX><16THENPRINT"No hi es !"
3690 REM roca
3700 IFG1=16ANDG2=26ANDX=3THENX=67
3710 IFG1=16ANDG2><26THENPRINT"Que dius que fas ?":GOTO600
3720 IFG1=12ANDG2><8ANDG2><10THENPRINT"Que dius ?":GOTO600
3730 IFG1=12ANDG2=10ANDX><SI%(10)ANDSI%(10)>0THENPRINT"No hi ha cap llibre !"
3740 IFG1=16ANDG2><26THENPRINT"Que dius ?":GOTO600
3750 IFG1=13ANDG2><25ANDG2><11ANDG2><15THENPRINT"Que dius que fas ?":GOTO600
3760 IFG1=8ANDG2=21ANDX=6THENX=14
3770 IFG1=8ANDG2=26ANDX=3THENX=67
3780 IFG1=15ANDG2=9ANDX><30ANDX><75THENPRINT"No hi ha penya-segat"
3790 IFG1=15ANDG2=9ANDX><30ANDX><75THENGOTO4270
3800 IFG1=15ANDG2=9ANDX=30THENX=29
3810 IFG1=15ANDG2=9ANDX=75THENX=74
3820 REM vaixells
3830 IFX=94ANDMAR=0ORX=94ANDPB=0THENGOTO5030
3840 REM cardona
3850 IFG1=17ANDG2=32ANDX><40THENPRINT"No es aqui !"
3860 IFG1=17ANDG2>32ORG1=17ANDG2<32THENPRINT"No es deixa !":GOTO4270
3870 IFG1=17ANDSI%(17)>0ANDSI%(18)>0THENPRINT"Amb que ?"
3880 REM mataplana
3890 IFG1=33ANDG2>29ANDG2<29THENPRINT"No creus que perds el temps ..."
3900 IFG1=33ANDG2=29ANDSI(7)>0ORG1=33ANDSI%(7)>0THENPRINT"amb que l'escrius ?"
3910 IFG1=3ANDG2=23ANDSI%(7)>0THENPRINT"Amb que els signes ?"
3920 REM mallorca
3930 IFX=95THEN4210
3940 REM anglaterra
3950 IFG1=3ANDG2><23THENPRINT"No volen autografs !"
3960 REM cofre
3970 IFG1=27ANDG2><14ANDG2><12THENPRINT"No pots !"
3980 IFG1=27ANDG2=14ANDX><CX%THENPRINT"No hi es !"
3990 REM escalada del castell
4000 IFG1=15ANDG2=34ANDSI%(16)=-1THENX=40
4010 IFG1=15ANDG2=34ANDX><40THENPRINT"A on vols pujar ?":GOTO4270
4020 IFG1=15ANDG2=34ANDSI%(16)>0THENPRINT"No tens cap escala !"
4030 IFG1=15ANDG2><9ANDG2><34ANDG2><19ANDG2><20THEN4050
4040 GOTO4060
4050 PRINT"No perds una mica el temps ?":GOTO4270
4060 IFG1=7THENIFG2=35ORG2=38THENIFX=91ORX=92THENIFDI<1THENPRINT"No tens prou ";
4070 IFG1=7THENIFG2=35ORG2=38THENIFX=91ORX=92THENIFDI<1THENPRINT"diners !"
4080 IFX<91ORX>92THENIFG1=7THENIFG2=35ORG2=38THENPRINT"No hi ha cap barquer !"
4090 IFG1=7THENIFG2=35ORG2=38THENIFX=91ORX=92THENIFDI>1THENDI=0:X=85
4100 IFG1=7ANDG2=36ANDX=14ANDDI<1THENPRINT"No tens prou diners !"
4110 IFG1=7ANDG2<35THENPRINT"No t'ho accepta"
4120 REM cova
4130 IFG1=22ANDG2=27ANDX=23THENX=49
4140 IFG1=22ANDG2><27THENPRINT"A on dius que vas ?"
4150 IFG1=6ANDG2><22THENPRINT"No es comestible !":G1=17:G2=33:GOTO3860
4160 IFG1=6ANDG2=22ANDSI%(22)>0THENPRINT"No tens queviures !":GOTO4270
4170 IFG1=12ANDG2=10ANDSI%(10)>0THENPRINT"No arribes a veure'l !"
4180 REM anell
4190 IFG1=27ANDG2=12ANDSI%(12)=XTHENPRINT"Pero si no el tens !"
4200 IFG1=36ANDG2><29THENPRINT"Que dius que recites ?"
4210 IFG1=15ANDG2=19ANDMAR=0THENPRINT"No et deixen, perque no es teva"
4220 IFG1=15ANDG2=20ANDMAR=0THENPRINT"No et deixen, perque no es teu"
4230 IFG1=13ANDG2=15ANDSI%(15)>0THENPRINT"No el tens a l'abas"
4240 IFG1=27ANDG2><12ANDG2><14THENGOTO4260
4250 GOTO4270
4260 PRINT"Ets un males mans d'intencions, pero no pots destapar-ho !"
4270 REM traba final
4280 REM
4290 IFG1=1THEN540
4300 PRINT"-------------------------------------"
4310 GOTO630
4320 REM
4330 REM preguntes
4340 REM
4350 IFD=11GOTO4410
4360 IFD=17GOTO4430
4370 IFD=24GOTO4450
4380 IFD=29GOTO4480
4390 IFD=33GOTO4500
4400 GOTO600
4410 PRINT:PRINT" Et trobes una noia i et pregunta :":PRINT" Saps informatica ?"
4420 D1=1:GOTO4520
4430 PRINT:PRINT" Et trobes un monjo que et pregunta :":PRINT" En que treballes ?"
4440 D1=1:GOTO4520
4450 PRINT:PRINT" Et trobes un soldat que et pregunta:":PRINT" Quina feina ";
4460 PRINT: PRINT" t'agradaria fer ?"
4470 D1=1:GOTO4520
4480 PRINT:PRINT" Et trobes un vagabund i et pregunta:":PRINT" Quina edat tens ?"
4490 D1=1:GOTO4520
4500 PRINT:PRINT" Et trobes un poeta que et pregunta :":PRINT" Quins estudis tens ?"
4510 D1=1:GOTO4520
4520 PRINT:PRINT" Que contestes ?":PRINT:D2=1:GOSUB4570:D2=0
4530 IFLEFT$(X$,2)="si"THENNI=3
4540 P$=P$+X$
4550 PRINT" ---------------------------------- "
4560 RETURN
4570 REM get important
4580 X$=""
4590 PRINT
4600 X$="":LOCATE,,1
4610 IFD2=1THENPRINT" ";
4620 IFD2=0THEN:COLOR1,15,15
4630 IFD2=3THEN:COLOR4,15,15
4640 IFD2=1THEN:COLOR8,15,15
4650 X1$=INKEY$
4660 IFX1$=""THEN4650
4670 SC=ASC(X1$):IFSC>122THEN4650
4680 SL=LEN(X$):IFSL>80THENRETURN
4690 IFSC>31THENX$=X$+X1$:PRINTX1$;:GOTO4620
4700 IFSC=13THENPRINT" ":GOSUB5090:LOCATE,,0:RETURN
4710 IFSC=20ANDSLTHENX$=LEFT$(X$,SL-1):PRINTX1$;
4720 GOTO4620
4730 REM ensambladora
4740 CLS:COLOR1,11,11:PRINTTAB(3)"L'enhorabona has trobat el que       cercaves :":PRINT
4750 PRINTTAB(3)" El COFRE i el seu contingut ...":PRINT:PRINT"  es un missatge ";
4760 PRINT"codificat que diu:":PRINT
4770 PRINTTAB(2)CHR$(34);MID$(L1$(X),10);:PRINTD+1100;SO;
4780 FORJ=1TOLEN(P$)
4790 IFP$=""THENP$=" "
4800 P1%=ASC(MID$(P$,J,1))+3
4810 PRINTCHR$(P1%);
4820 NEXTJ:PRINTCHR$(34):PRINT
4830 PRINTTAB(9)"Fes-ho saber al :"
4840 PRINT" CENTRE DIVULGADOR DE LA INFORMATCA"
4850 PRINTTAB(5)"GENERALITAT DE CATALUNYA"
4860 PRINTTAB(6)"Apartat de correus 5185":PRINTTAB(12)"Barcelona":PRINT
4870 PRINT" Escrivint el missatge i el teu nom i direccio. Gracies."
4880 PRINT
4890 GOTO5050
4900 REM farcell
4910 FA=0:FORJ=5TO40:IFSI%(J)=-1THENFA=FA+1
4920 NEXT
4930 IFMAR=1THENFA=1
4940 RETURN
4950 REM robatori
4960 PRINT"i que t'ho ha robat tot.":BA=0
4970 FORJ=5TO40
4980 IFSI%(J)<0THENSI%(J)=49
4990 NEXTJ:SI%(6)=SI%(6)+1:DI=0:RETURN
5000 REM la mort
5010 PRINT"I en no portar queviures t'has mort  de gana.":PRINT"Ho sento.":PRINT
5020 GOTO5050
5030 REM ofegada
5040 PRINT"T'HAS OFEGAT.":PRINT"Ho sento.":GOTO5050
5050 REM final
5060 PRINT"Si es vol reiniciar el joc polseu unatecla qualsevol."
5070 T$=INKEY$:IFT$=""THEN5070
5080 CLEAR:GOTO10
5090 BEEP:SOUND1,66:SOUND1,5
5100 SOUND2,254:SOUND8,10
5110 FORJ=0TO150:NEXT:SOUND8,0
5120 RETURN
